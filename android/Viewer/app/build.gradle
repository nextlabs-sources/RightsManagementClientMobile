apply plugin: 'com.android.application'
import org.apache.tools.ant.taskdefs.condition.Os

android {
    signingConfigs {
        config {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
            storeFile file('debug.keystore')
        }
    }
    compileSdkVersion 21
    buildToolsVersion "21.1.2"
    sourceSets.main {
        jniLibs.srcDir 'src/main/libs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }
    // call regular ndk-build(.cmd) script from app directory
    task ndkBuild(type: Exec) {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            if (System.env.ndkdir != null){
                def ndkBuild = new File(System.env.ndkdir, 'ndk-build.cmd')
                commandLine ndkBuild, '-C', file('src/main/jni').absolutePath
            }
        } else {
            if (System.env.ndkdir != null){
                def ndkBuild = new File(System.env.ndkdir, 'ndk-build')
                commandLine ndkBuild, '-C', file('src/main/jni').absolutePath
            }
        }
    }
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
    defaultConfig {
        applicationId "com.nextlabs.viewer"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
        ndk {
            moduleName "app"
            stl "gnustl_static"
            ldLibs "log", "z", "m"
            ///abiFilters "armeabi", "armeabi-v7a"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
}

dependencies {
    // Include DropBox SDK
    compile files('libs/dropbox-android-sdk-1.6.3.jar')
    // Include Microsoft OneDrive sdk as a dependency
    compile('com.onedrive.sdk:onedrive-sdk-android:1.0.2'){
        exclude group: 'com.android.support', module:'support-v4'
    }
    compile files('libs/json_simple-1.1.jar')
    compile files('libs/jcifs-1.3.18.jar')
    // Warning: Do Not Change Version Nums(7.8.0), or you will face Java.Lang.VerifyError
    compile 'com.google.android.gms:play-services-drive:7.8.0'
    compile 'com.google.api-client:google-api-client-android:1.20.0'
    compile 'com.google.api-client:google-api-client-gson:1.20.0'
    compile 'com.google.apis:google-api-services-drive:v2-rev170-1.20.0'
    // Include some Android official supports
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.android.support:recyclerview-v7:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.0'
    // what's the fucking of this?
    compile 'de.hdodenhof:circleimageview:1.2.1'
}
